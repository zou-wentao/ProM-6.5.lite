/*
 * Some parts of this file have been copied from JavaCC.jj under the following license:
 *
 * Copyright (c) 2006, Sun Microsystems, Inc.
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *     * Redistributions of source code must retain the above copyright notice,
 *       this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *     * Neither the name of the Sun Microsystems, Inc. nor the names of its
 *       contributors may be used to endorse or promote products derived from
 *       this software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF
 * THE POSSIBILITY OF SUCH DAMAGE.
 */

options
{
	STATIC=false;
	NODE_DEFAULT_VOID=true;
	NODE_PREFIX="Query";
	VISITOR=true;
	MULTI=true;
}
PARSER_BEGIN(QueryParser)
package org.processmining.xeslite.query.syntax;

import java.io.StringReader;

public class QueryParser {

    public QueryParser(String s) {
        this(new StringReader(s)); 
    }
    
}
PARSER_END(QueryParser)

SKIP :                              
{
    " "
    | "\t"
    | "\r"
    | "\n"

}

TOKEN [IGNORE_CASE]:
{
      <AND: "and">
   |  <OR:  "or">
   |  <LPAREN: "(">
   |  <RPAREN: ")">
   |  <EQUAL: "=">
   |  <CONTAINS: "%">
   |  <REGEX: "~">
   |  <NOTEQUAL: "!=">
   |  <GT: ">">
   |  <LT: "<">
   |  <LTE: "<=">
   |  <GTE: ">=">
   |  <DOT: ".">
}

/* JAVA LITERALS (from original JavaCC.jj project)*/

TOKEN :
{
  < INTEGER_LITERAL:
      ("-")?
      (  
      	<DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
      )
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
  	  ("-")?
  	  (
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
      )
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}


/* IDENTIFIER (adapted from JavaCC.jj) */

TOKEN :
{  
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>|<SPECIALCHAR>)* >
| < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"       
      ]
  >
| < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
| < #SPECIALCHAR:
 	  [
 	  	":",
 	  	"#",
 	  	"@",
 	  	",",
 	  	"_",
 	  	"-",
 	  	"|",
 	  	"&",
 	  	"^",
 	  	"$",
 	  	"#",
 	  	"@",
 	  	"+",
 	  	"*"
 	  ]
  >
}

<*> TOKEN : { <UNKNOWN: ~[]> }


QueryRoot parse() #Root :
{
}
{
    orExpression() <EOF> {return jjtThis;}
}

void orExpression() :
{	
}
{           
	andExpression()
	(							
		( jjtThis.op = <OR>.image andExpression()) #BinaryLogicalExpression(2)
	)*
}

void andExpression() :
{
}
{           
	term()
	(							
		( jjtThis.op = <AND>.image term()) #BinaryLogicalExpression(2)
	)*
}

void term() :
{
}
{
	simpleExpression()
    |    
    (<LPAREN> orExpression() <RPAREN>)
}

void simpleExpression() #SimpleExpression : 
{
}
{	
	( LOOKAHEAD(2) eventName() <DOT> )?	
   	attributeName()
   	(
   	 	jjtThis.op = op()
   	)    	
	value()
}

void eventName() #Atom : 
{
	Token val;
}
{
	(
		val = <IDENTIFIER> 
			{
				jjtThis.val = val.image;
			} 
		| 
		val = <STRING_LITERAL>
			{     
             jjtThis.val = val.image.substring(1, val.image.length() - 1);
         	}		
	)	
}

void attributeName() #Atom : 
{
	Token val;
}
{
	(
		val = <IDENTIFIER> 
			{
				jjtThis.val = val.image;
			} 
		| 
		val = <STRING_LITERAL>
			{     
             jjtThis.val = val.image.substring(1, val.image.length() - 1);
         	}		
	)	
}

void value() #Atom : 
{
	Token val;
}
{
	(
		( val = <IDENTIFIER> | val = <INTEGER_LITERAL> | val = <FLOATING_POINT_LITERAL> ) 
			{
				jjtThis.val = val.image;
			} 
		| 
		val = <STRING_LITERAL>
			{     
             jjtThis.val = val.image.substring(1, val.image.length() - 1);
         	}		
	)	
}

String op(): 
{
	Token op;
}
{
( op = <EQUAL> | op = <CONTAINS> | op = <REGEX> | op = <NOTEQUAL> | op = <GT> | op = <LT> | op = <LTE> | op = <GTE>)
	{
		return op.image;
	} 
}